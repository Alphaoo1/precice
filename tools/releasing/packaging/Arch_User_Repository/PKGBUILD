# Maintainer:  The preCICE developers <info@precice.org>

pkgname=precice
pkgver=2.1.0
pkgrel=1
pkgdesc="A Coupling Library for Partitioned Multi-Physics Simulations on Massively Parallel Systems"
arch=('x86_64')
url="https://www.precice.org"
license=('LGPL3')
depends=('openmpi' 'boost')
makedepends=('cmake' 'eigen')
optdepends=('petsc: Parallel RBF mapping (GMRES solver) support'
            'python-numpy: Python actions support')
provides=('precice')
source=("https://github.com/precice/precice/archive/v${pkgver}.tar.gz")
sha256sums=('1e6432724f70d0c6c05fdd645e0026754edbc547719a35bf1d3c12a779b1d00e')

build() {
    cd "${pkgname}-${pkgver}"
    cmake . \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DBUILD_SHARED_LIBS=ON \
          -DPRECICE_PETScMapping=on \
          -DPRECICE_PythonActions=on
    make
}

check() {
    cd "${pkgname}-${pkgver}"
    make test
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}/" install
}
